pluginManagement {
    def flutterSdkPath = {
        def properties = new Properties()
        file("local.properties").withInputStream { properties.load(it) }
        def flutterSdkPath = properties.getProperty("flutter.sdk")
        assert flutterSdkPath != null, "flutter.sdk not set in local.properties"
        return flutterSdkPath
    }()

    includeBuild("$flutterSdkPath/packages/flutter_tools/gradle")

    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
}

plugins {
    id "dev.flutter.flutter-plugin-loader" version "1.0.0"
    id "com.android.application" version "8.6.0" apply false
    id "org.jetbrains.kotlin.android" version "2.1.0" apply false
}

include ":app"

// Workaround for plugins using flutter.compileSdkVersion (geolocator_android, etc.)
// Provide flutter extension to subprojects before evaluation
class FlutterExtension {
    int compileSdkVersion = 35
    int targetSdkVersion = 35
    int minSdkVersion = 21
    String ndkVersion = "26.1.10909125"
}

gradle.beforeProject { project ->
    // Skip :app - it has its own flutter extension from Flutter plugin
    if (project.name != 'app' && !project.extensions.findByName('flutter')) {
        project.extensions.create('flutter', FlutterExtension)
    }
}
